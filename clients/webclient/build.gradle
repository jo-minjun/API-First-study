plugins {
    id 'java'
    id 'java-library'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'org.openapi.generator'
    id 'maven-publish'
}

def libraryName = 'webclient'
def specPath = "${parent.rootDir}/src/main/resources/api.yaml"
def outputPath = "${buildDir}/generated/java-${libraryName}-client"

group = 'minjun.client'
version = '0.0.1-SNAPSHOT'
archivesBaseName = "${libraryName}-client"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'io.swagger:swagger-annotations:1.6.2'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.3'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

openApiGenerate {
    generatorName = "java"
    inputSpec = specPath
    outputDir = outputPath
    apiPackage = "minjun.openapi.api"
    modelPackage = "minjun.openapi.api.model"
    modelNameSuffix = "Dto"
    apiFilesConstrainedTo = [""]
    modelFilesConstrainedTo = [""]
    supportingFilesConstrainedTo = [""]
    configOptions = [
            title: "webclient",
            useTags: "true",
            dateLibrary: "java8",
            java8: "true",
            hideGenerationTimestamp: "true",
            library: "${libraryName}",
            serializableModel: "true",
            serializationLibrary: "jackson",
            bigDecimalAsString: "true",
            useRuntimeException: "true"
    ]

    validateSpec = true
}

sourceSets {
    main {
        java {
            srcDir file("${outputPath}/src/main/java")
        }
    }
}

jar {
    enabled = true
    from sourceSets.main.output
}

bootJar {
    enabled = false
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = archivesBaseName

            from components.java
        }
    }
}

compileJava.dependsOn("openApiGenerate")